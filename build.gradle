import com.anysoftkeyboard.tools.makedictionary.MakeDictionaryTask

buildscript {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'

        classpath 'com.github.AnySoftKeyboard.AnySoftKeyboardTools:makedictionary:99bd3e6'
        classpath 'org.jsoup:jsoup:1.9.1'
    }
}

apply plugin: 'checkstyle'
apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.anysoftkeyboard.languagepack.ukrainian"
        minSdkVersion 7
        targetSdkVersion 25
        versionCode 100
        versionName "2.0"
    }

    signingConfigs {
        release {
            storeFile file("key.store")
            storePassword System.getenv("PACK_KEYSTORE_PASSWORD")
            keyAlias System.getenv("PACK_KEYSTORE_ALIAS")
            keyPassword System.getenv("PACK_KEYSTORE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            zipAlignEnabled true
            debuggable false

            minifyEnabled false
        }
    }
}

task makeDictionary(type: MakeDictionaryTask) {
  doFirst {
    mkdir 'src/main/res/raw'
  }
  inputWordsListFile new File(project.getProjectDir(), "dictionary/words.xml")
}

afterEvaluate { proj ->
    proj.tasks.all { task ->
        if (task.name.startsWith('generate') && task.name.endsWith('BuildConfig')) {
            task.dependsOn makeDictionary
        }
    }
}

dependencies {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
    compile 'com.github.AnySoftKeyboard:AnySoftKeyboard-API:1.7.0'
}

task checkstyle(type: Checkstyle) {
    configFile file("${projectDir}/checkstyle/checkstyle.xml")
    source 'src'
    classpath = files()
}